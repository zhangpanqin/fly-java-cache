// buildcript 定义 gradle 自身的配置
//buildscript {}
plugins {
    id 'java'
    id 'idea'
    id 'org.springframework.boot' version "${springBootVersion}" apply false
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
}
ext {
    guavaVersion = '31.1-jre'
    commonsLang3Version = '3.12.0'
    commonsCollections4Version = '4.4'
    mapstructVersion = '1.4.2.Final'
    lombokMapstructVersion = '0.2.0'
    hutoolVersion = '5.7.22'
    commonsPool2Version='2.11.1'
}
allprojects {
    java {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    repositories {
        mavenLocal()
        maven {
            name = 'maven center 和 jcenter 阿里云'
            url = 'https://maven.aliyun.com/repository/public'
        }
        maven {
            name = 'spring 阿里云'
            url = 'https://maven.aliyun.com/repository/spring'
        }
        maven {
            name = 'google 阿里云'
            url = 'https://maven.aliyun.com/repository/google'
        }
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'

    configurations.all {
        resolutionStrategy {
            cacheChangingModulesFor 0, 'seconds'
            force "org.apache.commons:commons-lang3:${commonsLang3Version}"
            force "org.apache.commons:commons-collections4:${commonsCollections4Version}"
            force "com.google.guava:guava:${guavaVersion}"
            force "cn.hutool:hutool-all:${hutoolVersion}"
            force "org.apache.commons:commons-pool2:${commonsPool2Version}"
        }
    }

    configurations {
        parentBom
        runtimeOnly.extendsFrom(parentBom)
        compileOnly.extendsFrom(parentBom)
        annotationProcessor.extendsFrom(parentBom)
        implementation.extendsFrom(parentBom)
    }

    dependencies {
        parentBom platform(org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES)
        implementation 'org.apache.commons:commons-lang3'
        implementation 'cn.hutool:hutool-all'
        implementation 'org.apache.commons:commons-collections4'
        implementation 'com.google.guava:guava'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor "org.mapstruct:mapstruct-processor:$mapstructVersion"
        annotationProcessor 'org.projectlombok:lombok'
        annotationProcessor "org.projectlombok:lombok-mapstruct-binding:$lombokMapstructVersion"
        annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    }


    tasks.named('test') {
        useJUnitPlatform()
    }
}

wrapper {
    gradleVersion = '7.4.1'
}

apply from: "${rootProject.projectDir}/gradle/git-hooks/git-hooks.gradle"